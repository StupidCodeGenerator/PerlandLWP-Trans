# Chapter 2. Web Basics

HTML协议, HTTP传输， URL标识使web成为了可能.为了从web页面抓取信息， 我们必须知道以下三点：
构建一个你想访问某个页面的url，创建HTTP请求并且解码HTTP的响应,解析HTML提取信息.
这一章涉及到URL的构建以及HTTP的概念.HTML解析比较复杂在后续的章节会讲解.
你将会学习到如何使用LWP::Simple自动化处理最基本的网络作业, 正如他名字提到那样,这个模块的
接口非常简单.你将会学习到这个模块接口有哪个局限,并且使用其他LWP模块打破这种局限.

### 2.1 URLs
一个Url代表网络上某个地方的地址. 举个例子:
http://www.oreilly.com/news/bikeweek_day1.html
Url是一种结构,在RFC 2396规范中有定义, 长达40页,因为构造URL的方式多种多样.因为我们只关心HTTP
和FTP的URL,使用分隔符来组成URL:
```sh
scheme://username@server:port/path?query
```
在上面的例子中, scheme 是 http, server 是 www.oreilly.com, path是 /news/bikeweek_day1.html
以下是一个FTP URL:
    ftp://ftp.is.co.za/rfc/rfc1808.txt
scheme 是 ftp, 主机名ftp.is.co.za, path是/rfc/rfc1808.txt,scheme和主机名是不区分大小写的,
但是其他的是区分大小写的.ftp://ftp.is.co.za/rfc/rfc1808.txt 和 fTp://ftp.Is.cO.ZA/rfc/rfc1808.txt
是相同的地址.但是ftp://ftp.is.co.za/rfc/rfc1808.txt 和 ftp://ftp.is.co.za/rfc/RFC1808.txt 不相同.
除非服务器在请求的时候忽略大小写差异.

我们可以忽略掉不指定任何东西的URL,web客户端可以帮忙恢复它.举个例子:telnet://melvyl.ucop.edu/标明了一个你可以
开始Telnet会话的主机名,mailto:mojo@jojo.int标明了一个邮箱地址.
我们只能使用字符A到Z, a到z,0到9和以下标点符号来构造URL.
```sh
-     _     .     !     ~     *     '     ,
:     @     &     +     $     (     )     /
```
对于查询组件, 规则也是一样的,只有以下标点可以:
```sh
-     _     .     !     ~     *     '     (     )
```
其他的字符必须通过URL编码,i.e:通过一个百分号后跟两个十六进制数来表示一个字符.如果你想在URL中使用空格
通过%20来表示,因为空格的ASCII码是32,十六进制的0x20.
有时你会在URL中看到以下字符：
```sh
{     }    |    \    ^    [    ]    `
```
RFC2396文档中指出，使用这些字符是不可靠，不明智的.当遇到不安全字符时,也会编码.
URL查询组件赋值如下：
name=Hiram%20Veeblefeetzer&age=35&country=Madagascar
在查询语句中有3个参数:name值为"Hiram Veeblefeetzer"(空格字符被编码);age值为35,country值为"Madagascar".

URI::Escape模块提供了uri_escape()接口来创建URLs:
```perl
use URI::Escape;
encoded_string = uri_escape(raw_string);
```

举个例子,创建查询name,age和country的字符串:
```perl
$n = uri_escape("Hiram Veeblefeetzer");
$a = uri_escape(35);
$c = uri_escape("Madagascar");
$query = "name=$n&age=$a&country=$c";
print $query;
name=Hiram%20Veeblefeetzer&age=35&country=Madagascar
```


### 2.2 一次HTTP传输
超文本传输协议（HTTP）主要用来从web服务器上抓取文档。RFC 2616文档中有关 于HTTP的定义
但是这一节主要讲解使用LWP需要知道的一些事情。

HTTP是一种服务器/客户端协议： 文件在服务器上，客户端来访问。在常规的网页 浏览中，客户端通常是向Mozilla或者IE这样的浏览器。
URL指定了一个与浏览器通讯并且可以请求文档的服务器。服务器要么返回失败 （“文件没找到”）要么返回成功（“请求的文档”）

例子 2-1 一次简单的客户端请求
```sh
GET /daily/2001/01/05/1.html HTTP/1.1
Host: www.suck.com
User-Agent: Super Duper Browser 14.6
[blank line]
```

例子2-2给出了一次结果为成功的响应
```sh
HTTP/1.1 200 OK
Content-type: text/html
Content-length: 24204
[blank line]
[and then 24,204 bytes of HTML code]
```

例子2-3给出一次失败的响应
```sh
HTTP/1.1 404 Not Found
Content-type: text/html
Content-length: 135

<html><head><title>Not Found</title></head><body>
Sorry, the object you requested was not found.
</body><html>
[and then the server closes the connection]
```


#### 2.2.1 请求

一次HTTP请求包括三个部分: request line , headers, body

request line 指出客户端想要做什么（method), 在哪做（path）以及使用什么协 议。虽然HTTP协议定义了很多方法，最常用的也就是GET和POST。路径是URL请求的 一部分
（在例子2-1中路径为/daily/2001/01/05/1.html)。协议版本通常是HTTP/1.1。

每一个header都包含一个键和一个值（举个例子：User-Agent: SuperDuperBrowser/14.6)。在HTTP 1.1版本之前，header是可选的。在HTTP 1.1,header必须存在，用来给当前与服务器通信的浏览器命名。这是URL请求的 server部分(eg., www.suck.com )。header必须以一个空白行终止，无论是否有 header它都必须存在。

body部分可以包含任意数据。当body发送的时候， request中的 Content-Type和 Content-Length 头会帮助服务器解码数据。GET查询没有任何附加数据，所以这个 部分为空。对于我们而言，
只有POST查询包含HTTP请求的body部分。

以下是一份HTTP请求中最重要的头信息
```sh
Host: www.youthere.int
```
这个头必须存在用来告诉服务器通过URL请求的主机名。
告诉服务器自己的名字这也许听起来很奇怪，但是这个head在HTTP1.1中添加是为 了处理一台HTTP服务器回应多个不同主机的情况。

```sh
User-Agent: Thing/1.23 details...
```

这个可选头指定了浏览器的品牌和型号。对于一个可用的浏览器，通常是 Mozilla/4.76 [en] (Win98; U)或者Mozilla/4.0 (compatible; MSIE 5.12; Mac_PowerPC)这样的信息。默认情况下，LWP发送User-Agent头为libwww-perl/5.64(或者任何一个你确认的LWP版本）。

```sh
Referer: http://www.thingamabob.int/stuff.html
```
这个可选的header告诉远程服务器这个正在被请求的page的URL。
"Referrer"这个拼写更加准确，但是"Referer"在多年以前已经在规范中固定下来了。

```sh
Accept-Language: en-US, en, es, de
```
这个可选的header告诉远程服务器用户浏览内容所使用的语言，使用语言标签来表示。比如，上面的列表的意思是说用户更倾向于使用美式英语，或者其他种类的英语，西班牙语，或者德语。（附录D，"Language Tags"列表说明了最通用的语言标识。）许多浏览器不发送这个header，它们通常发送用户安装的浏览器版本。举个例子，如果浏览器是西班牙语版本的Netscape，浏览器也许会发送 Accept-Language:es, 除非用户在浏览器设置菜单中修改其他语言。

"www.youthere.int" ? 是的，这有一个 ".int"的域名。这是国际条约组织所使用的（像世界卫生组织或者北约），这意味着这域名永久有效,并且非常巧妙。所以我在本书中广泛的使用这个假的主机名。

当我写此书的时候我还不知道，RFC 2606已经预留了example.com/.net/.org 让其他web书籍和文档用作例子使用。

#### 2.2.2. 响应

The server's response also has three parts: the status line, some headers, and an optional body.
服务器响应也包含三个部分：status line， 一些header和可选的body。


status line 指出服务器通信的协议状态，然后给出一个状态值和一小段消息。比如，"HTTP/1.1 404 Not Found." 状态值在200-299之间表示成功，400-499以及其他值表示失败。完整的状态值列表在附录B "HTTP Status Code"中给出。


header 可以让服务器包含关于响应请求的额外信息。比如，如果需要身份验证，服务器使用header来表明身份验证类型。最经常遇到header，是Content-Type显示请求成功与失败，用来帮助浏览器解析body。Headers以一个空白行结束，无论是否有header都要有空白行。

许多响应都包含一个Content-Length用来指定长度，body的字节数。然而，这个在动态生成的页面中很少出现，因为你永远不知道哪个页面将会动态生成，所以就别指望存在这个字段了。

（其他少见的 header 有的在URL指定指定内容，或者服务器需要浏览器发送HTTP Cookies中使用等等;然而，这些通常都可以通过LWP自动处理。）


响应的body后有一个空行可以是任意数据。在一个典型的web请求中，通常是显示的HTML文档。如果遇到错误，信息body通常包含服务器生成的错误信息（通常是HTML，但是有时不是）用来解释错误。

### 2.3. LWP::Simple 模块

HTTP请求中，GET请求是最简单并且最常用的。形参可以写在URL中，但是不会有body参数。LWP::Simple 模块有许多函数可以通过GET请求快速的获取到文档。这些函数可以用来处理文档的获取，保存和打印。

#### 2.3.1. 基本文件获取

LWP::Simple模块get()函数需要一个URL作为参数，返回文件的body。

```sh
$document = get("http://www.suck.com/daily/2001/01/05/1.html");
```
如果获取不到文件，get()返回undef。如果 LWP请求的URL或者服务器答复他们已经迁到其他的URL，LWP将会请求那个URL并返回。


如果只使用 LWP::Simple get()方法，我们不能给get请求设置header，并且也得不到很多像状态值这样的额外信息。很重要的一点是,因为很多web服务器同样的文档使用了不同语言并且使用http language header来决定返回哪个文件。同样的，HTTP响应码可以让我们区分是永久性故障（e.g. 404 Not Found）还是暂时性的(505 Service [Temporarily] Unavailable)。

即使是普通的网络机器人中最常见的类型（链接检测者），也受益于响应码。403（"Forbidden", 通常是文件权限引起的） 可以被自动校正，然而404（""）错误意味着是一个需要被修改的过期链接。如果你想接收到这些返回值或者响应中其他的主要内容，你的任务将不再简单，所以你不能再使用LWP::Simple了。在LWP::Simple中"简单"并非只有接口风格，还有所完成的任务。


2.3.2. Fetch and Store
#### 2.3.2 获取与保存

有一种方法可以获取到状态值是使用LWP::Simple的getstore()方法，这个接口需要把文档写到一个文件中并返回请求的状态值:

```sh
$status = getstore("http://www.suck.com/daily/2001/01/05/1.html",
                   "/tmp/web.html");
```
这里有两个问题。第一，文档储存在了可操作的文件中（提取信息，转换格式等等）而不是在变量中。这样使用perl内置的open()和<FH>操作来读取文件简直轻而易举;见下面的例子。

另一个问题是这个状态值并非特别有用：如何知道这值代表的是成功？也就是说这个文件包含请求的文档吗？ LWP::Simple提供了is_success() 和 is_error()函数来解决这个问题：

```sh
$successful = is_success(status);
$failed     = is_error(status);
```

如果这个状态值代表的是成功（范围在200-299之间），is_success()返回true。如果遇到一个错误（400-599），is_error()返回true。例如，这段代码保存着BookTV(CSPAN2)的上市时间表如果提到Gore Vidal就输出一条消息：

```sh
use strict;
use warnings;
use LWP::Simple;
my $url  = 'http://www.booktv.org/schedule/';
my $file = 'booktv.html';
my $status = getstore($url, $file);
die "Error $status on $url" unless is_success($status);
open(IN, "<$file") || die "Can't open $file: $!";
while (<IN>) {
  if (m/Gore\s+Vidal/) {
    print "Look!  Gore Vidal!  $url\n";
    last;
  }
}
close(IN);
```

#### 2.3.3 获取与输出

LWP::Simple 也导出了getprint()函数:
```sh
$status = getprint(url);
```

文档输出到了当前的output文件句柄中（通常是 STDOUT）。在其他方面，它的行为就像 getstore()。这个也可以非常方便的在一行中处理：

```sh
% perl -MLWP::Simple -e "getprint('http://cpan.org/RECENT')||die" | grep Apache
```
检索"http://cpan.org/RECENT"，获取过去一周上传到cpan的列表（是非常清楚的txt文件，不是HTML），然后输出到STDOUT，过滤包含"Apache"的行。


#### 2.3.4. HEAD预览

LWP::Simple 也导出了head()函数，用来询问服务器，"如果我要通过GET请求某个事项，将要使用什么header?"当我们需要检查链接时这个函数会非常有用。虽然并不是所有的服务器都支持HEAD请求，如果header()返回真，那就基本上可以确定服务器支持HEAD请求（可是，如果head()返回失败，有可能服务器不支持HEAD请求）。

head()返回值依赖与他请求的是标量上下文还是列表上下文。如果是标量上下文，这很简单：
```sh
$is_success = head(url);
```
如果服务器对于HEAD请求返回一个成功的状态值，返回真。否则，返回假。你也可以这么用:

```sh
die "I don't think I'll be able to get $url" unless head($url);
```

遗憾的是，一些老的服务器，还有大多数使用CGI的较新的服务器，都不认识HEAD请求。这种情况下，他们回应"405 Method Not Allowd"信息，但是如果你已经执行了GET请求，会有一些实际的回应。仅靠head()所提供的少量信息，真的无法处理如上的任何一种情况，因为我们在请求失败的情况下无法获得状态值也无法获得内容（理论上是什么都没有）。

在列表上下文中，head()返回一个5个值的列表，如果请求成功:
```sh
(content_type, document_length, modified_time, expires, server)
    = head(url);
```
content_type是表单type/subtype中的MIME类型字符串;最常用的MIME类型在附录C "Common MIME Types"中列出。document_length 在Content-Length header中，如果存在，应该是请求到的文档的字节数。modified_time 是Last-Modified 头转换成数字的内容，就好像使用perl中的time()函数一样。对于通常的文件（GIF,HTML等等），Last-Modified仅仅是文件的修改时间，但是动态生成的内容通常不会有Last-Modified头。


最后两个值很少用到;expires 代表从使用开始到数据不再有效的时间（网页缓存时间）。server代表服务器发送的header的内容，来告诉我们运行的什么类型的软件。一个典型值是“Apache/1.3.22 (Unix)“。

在列表上下文中，一次不成功的请求返回空列表。所以当你拷贝返回的列表到一堆标量中,他们都被赋值为undef。你可以不必复制所有的值，可以只保存之前的一部分，像例子2-4。

```sh
use strict;
use LWP::Simple;
foreach my $url (
  'http://us.a1.yimg.com/us.yimg.com/i/ww/m5v9.gif',
  'http://hooboy.no-such-host.int/',
  'http://www.yahoo.com',
  'http://www.ora.com/ask_tim/graphics/asktim_header_main.gif',
  'http://www.guardian.co.uk/',
  'http://www.pixunlimited.co.uk/siteheaders/Guardian.gif',
) {
  print "\n$url\n";

  my ($type, $length, $mod) = head($url);
  # so we don't even save the expires or server values!

  unless (defined $type) {
    print "Couldn't get $url\n";
    next;
  }
  print "That $type document is ", $length || "???", " bytes long.\n";
  if ($mod) {
    my $ago = time( ) - $mod;
    print "It was modified $ago seconds ago; that's about ",
      int(.5 + $ago / (24 * 60 * 60)), " days ago, at ",
      scalar(localtime($mod)), "!\n";
  } else {
    print "I don't know when it was last modified.\n";
  }
}
```

现在,运行后程序打印如下：
```sh
http://us.a1.yimg.com/us.yimg.com/i/ww/m5v9.gif
That image/gif document is 5611 bytes long.
It was modified 251207569 seconds ago; that's about 2907 days ago, at Thu Apr 14 18:00:00 1994!

http://hooboy.no-such-host.int/
Couldn't get http://hooboy.no-such-host.int/

http://www.yahoo.com
That text/html document is ??? bytes long.
I don't know when it was last modified.

http://www.ora.com/ask_tim/graphics/asktim_header_main.gif
That image/gif document is 8588 bytes long.
It was modified 62185120 seconds ago; that's about 720 days ago, at Mon Apr 10 12:14:13 2000!

http://www.guardian.co.uk/
That text/html document is ??? bytes long.
I don't know when it was last modified.

http://www.pixunlimited.co.uk/siteheaders/Guardian.gif
That image/gif document is 4659 bytes long.
It was modified 24518302 seconds ago; that's about 284 days ago, at Wed Jun 20 11:14:33 2001!
```

如果你正在使用非常著名的CGI.pm模块，注意他也有一个函数叫head()。为了避免冲突，你可以告诉 LWP::Simple导出所有函数除了head():
```sh
use LWP::Simple qw(!head);
use CGI qw(:standard);
```

如果没有 qw(!head)这句，LWP::Simple将会导出head(), CGI也会导出head()（在模块中的:standard中），将会挂掉，产生一个像“Prototype mismatch:sub main::head ($) vs none.” 这样的隐秘警告。因为任何使用CGI库的程序都是一个CGI脚本，任何一个警告（或者，事实上，任何定向到STDERR）都足够让CGI终止的附带一个“500 Internal Server Error"的错误信息。

### 2.4. 不使用LWP::Simple 抓取文档

LWP::Simple 非常实用但是功能并不强大。尤其是，我们不能创建POST请求或者给请求设置header信息或者查询响应header信息。为了做这些事，我们必须跳出LWP::Simple。

通常用来做HTTP GET的万能方法是使用do_GET()函数，子程序如例子2-5。

```sh
use LWP;
my $browser;
sub do_GET {
  # Parameters: the URL,
  #  and then, optionally, any header lines: (key,value, key,value)
  $browser = LWP::UserAgent->new( ) unless $browser;
  my $resp = $browser->get(@_);
  return ($resp->content, $resp->status_line, $resp->is_success, $resp)
    if wantarray;
  return unless $resp->is_success;
  return $resp->content;
}
```

do_GET()程序的完成讲解在单元3 "The LWP Class Model"中给出。现在我们只需要用而不用完全理解他是如何工作的。

你既可以在标量上下文又可以在列表上下文中调用do_GET()函数:

```sh
doc = do_GET(URL [header, value, ...]);
(doc, status, successful, response) = do_GET(URL [header, value, ...]);
```

在标量上下文中，他发那会请求的文档 如果遇到错误返回undef。在列表上下文中，返回文件内容，HTTP请求的状态行，和一个标识状态值来表示响应是否成功，和一个我们可以获取更多关于响应信息的对象。

设置成undef来忽略它的部分返回值。例如，一下是如何获取文档中的字符并且获知是否成功：
```sh
($doc, undef, $successful, undef) = do_GET('http://www.suck.com/');
```

header和header值对于do_GET()来说是可选的可以让我们增加header到请求中。例如，尝试获取从欧盟主页获取德语版本：

```sh
$body = do_GET("http://europa.eu.int/",
  "Accept-language" => "de",
);
```
这个章节所使用的do_GET像LWP::Simple get()函数一样简单方便，但是没有get()函数的一些限制。



### 2.6. HTTP POST

有些表单使用GET请求来提交参数到服务器上，但是更多则是用的POST。不同的是POST请求通过请求的body来传递参数，而GET请求通过给参数编码插入到URL中。
Babelfish(http://babelfish.altavista.com)是一个可以让你可以使用人类语言来传输文本的服务。如果你通过浏览器使用Babelfish，你会看到贴着你要翻译的文本的表单，指定你想从哪种语言翻译成那种，然后翻译。过了一会，一个翻译完的新页面生成了。

在后台，浏览器输入一个 键/值对到一个表单中：
```sh
urltext = I like pie
lp = en_fr
enc = utf8
```
然后把他们整理进HTTP请求中：
```sh
POST /translate.dyn HTTP/1.1
Host: babelfish.altavista.com
User-Agent: SuperDuperBrowser/14.6
Content-Type: application/x-www-form-urlencoded
Content-Length: 40

urltext=I%20like%20pie&lp=en_fr&enc=utf8
```
这就好像我们使用do_GET()函数来自动化一个GET查询，例子2-7使用do_POST()函数来自动化POST查询。
```sh
use LWP;
my $browser;
sub do_POST {
  # Parameters:
  #  the URL,
  #  an arrayref or hashref for the key/value pairs,
  #  and then, optionally, any header lines: (key,value, key,value)
  $browser = LWP::UserAgent->new( ) unless $browser;
  my $resp = $browser->post(@_);
  return ($resp->content, $resp->status_line, $resp->is_success, $resp)
    if wantarray;
  return unless $resp->is_success;
  return $resp->content;
}
```

使用do_POST():
```sh
doc = do_POST(URL, [form_ref, [headers_ref]]);
(doc, status, success, resp) = do_POST(URL, [form_ref, [headers_ref]]);
```
在列表上下文和标量上下文中的返回值与do_GET()一样。form_ref参数是一个包含表单参数的hash的引用。headers_ref参数是一个包含你打算发送请求的header的hash的引用。






