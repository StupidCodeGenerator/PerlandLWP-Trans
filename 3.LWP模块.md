
为了获取HTTP事务请求方方面面的完整信息-你不得不使用比LWP::Simple更强大的模块，使用以面向对象作为核心思想的LWP套件。这个单元介绍使用LWP作为浏览器对象（使用它来发送请求）和响应对象（得到请求的返回结果）。你可以学习到使用基本的方法来构造请求和检查响应结果，这些技能将会在后续的章节如cookies,语言选择，爬虫等中用得到。

### 3.1. 基本模块 (The Basic Classes)

在LWP模块中，通过LWP::UserAgent中的浏览器对象执行GET,HEAD,POST请求，处理HTTP响应结果的模块叫HTTP::Response。这两个是主要对象，还有一些其他模块提供cookie管理或者用户代理功能可以用来做爬虫。还有一些对象用来处理非HTTP方向的web,像HTML模块。在这一章，我们使用这些模块来执行web请求。

逐个添加如下模块：
```sh
use LWP::UserAgent;
use HTTP::Response;
```
但是加载LWP::UserAgent和LWP::Response 使用如下方法最简单方便：
```sh
use LWP;               # same as previous two lines
```

如果你熟悉perl的面向对象编程，LWP模块会带给你一些真正的惊喜。你只需要知道这些基本的对象和访问函数的名字就可以了。如果你不了解面向对象编程，你需要掌握一下。附录G，“User's View of Object-Oriented Modules”将会给你提供一些面向对象的概念。想学习更多（包括如何写自己的对象），可以查看Programming Perl(O'Reilly)。


### 3.2. 使用LWP模块编程

第一步是初始化浏览器对象，他可以被用在程序中的其他地方。我们需要一个浏览器对象来执行HTTP请求，虽然可以在一个程序中使用多个浏览器对象，但我现在还没遇到使用多个的情况。
浏览器对象可以使用代理（一个帮你抓取web页面的服务器，例如防火墙，或者一个像Squid的web缓存）。使用calling_env_proxy()来检查环境中是否设置过代理：

```sh
use LWP::UserAgent;
my $browser = LWP::UserAgent->new( );
$browser->env_proxy( ); # if we're behind a firewall
```
对于大多数的用户代理这是全部的初始化了。当你做完了这些以后，除了get(), head(), post()，这些以外没什么可做的了。例如：
```sh
$url = 'http://www.guardian.co.uk/';
my $response = $browser->get($url);
```

然后你可以调用一些方法来检查响应状态，提取内容等等。例如，以下代码用来保证我们可以成功的抓取html文档而不必担心被截断，然后输除一段信息告诉我们"Madonna"或者"Arkansas"是否在文件内容中出现过：

```sh
die "Hmm, error \"", $response->status_line( ),
  "\" when getting $url"  unless $response->is_success( );
my $content_type = $response->content_type( );
die "Hm, unexpected content type $content_type from $url"
   unless $content_type eq 'text/html';
my $content = $response->content( );
die "Odd, the content from $url is awfully short!"
   if length($content) < 3000;
if($content =~ m/Madonna|Arkansas/i) {
   print "<!-- The news today is IMPORTANT -->\n",
         $content;
} else {
   print "$url has no news of ANY CONCEIVABLE IMPORTANCE!\n";
}
```

正如你所看到的一样，响应对象包含从服务器请求到的所有数据（或者一个关于访问浏览器失败的错误信息），然后我们使用方法来获得数据。有一些储存器来保存响应数据的不同部分（eg.状态行），然后有一个非常方便的函数告诉我们响应是否成功（is_success())。

这就是一个可工作的完整的LWP程序了！


### 3.3. do_GET , do_POST 函数探究

通过第2章“web基础”，我们已经了解了do_GET()和do_POST()函数。让我们先看do_GET()函数。

先加载模块，然后定义$browser变量来保存user agent。这个定义在do_GET()子程序之外，所以本质上是静态变量，在调用子程序时保存数值。例如，如果你要支持HTTP cookies，这个brower可以在调用do_GET()之后被保存下来，and cookies set by the server in one call would be sent back in a subsequent call.
```sh
use LWP;
my $browser;
sub do_GET {
```
下一步，如果用户代理不存在就创建一个：
```sh
$browser = LWP::UserAgent->new( ) unless $browser;
```
允许代理，如果你有防火墙的话：

```sh
$browser->env_proxy();
```
然后使用子程序的参数来执行GET请求：
```sh
my $response = $browser->request(@_);
```

在列表上下文，返回响应对象提供的信息：内容，状态行，一个标识请求是否成功的布尔值，和响应对象本身：
```sh
return($response->content, $response->status_line, $response->is_success, $response)
  if wantarray;
```
如果在标量上下文调用中出来了错误，返回undef ：
```sh
return unless $response->is_success;
```
否则我们返回内容：

```sh
  return $response->content;
}
```
do_POST()子程序和do_GET()程序一样，只是使用了post()替换了get()。
这个章节的其余部分主要是LWP::UserAgent和HTTP::Response的详细介绍。


### 3.4. 用户代理 (User Agents)

第一个最简单的两个基本类是LWP::UserAgent中用来管理HTTP连接和执行请求。new()构造函数创建一个用户代理对象：
```sh
$browser = LWP::UserAgent->new(%options);
```
options的默认值在表3-1中给出了。 将在下一节来描述这些属性值的获取和修改的函数调用。

Key                     | Default            |
------------------------|--------------------|
agent 	                | "libwww-perl/#.###"|
conn_cache 	            | undef              |
cookie_jar 	            | undef              |
from 	                | undef              |
max_size 	            | undef              |
parse_head 	            | 1                  |
protocols_allowed 	    | undef              |
protocols_forbidden 	| undef              |
requests_redirectable 	| ['GET', 'HEAD']    |
timeout 	            | 180                |

如果你已经有了一个用户代理对象并且想要复制一个(例如，你想要在多个连接中发送相同的请求，一个是持久连接， 一个不是)使用clone()方法：
```sh
$copy = $browser->clone();
```
这个对象看起来像一个浏览器并且可以通过很多方法来获取或者设置属性。连接属性的修改（e.g.代理, 超时时间, 或者是否是持久连接）或者连接请求的修改（e.g.认证，cookies，或者HTTP头）。



#### 3.4.1. Connection Parameters

timeout()函数用来设置LWP需要等待服务器响应的最长时间：
```sh
$oldval = $browser->timeout([newval]);
```
如果你想设置一个值，可以如下做：
```sh
$browser->timeout(newval);
```
如果你想读取这个值，可以如下做：
```sh
$value = $browser->timeout( );
```
也可以同时设置新的值返回之前设置过的值：
```sh
$previously = $browser->timeout(newval);
```
超时属性的默认值是180秒。如果你在用爬虫，你可能需要设置少一点，用来防止爬虫在一个废弃的站点上浪费太长时间：
```sh
$oldval = $browser->timeout( );
$browser->timeout(10);
print "Changed timeout from $oldval to 10\n";
```
从180修改为10

max_size()方法限制客户端可以读到HTTP请求的最大字节数：
```sh
$size = $browser->max_size([bytes])
```

max_size()属性的默认值是undef，表示没有限制。如果响应字节数超过了最大值，响应会有一个"Client-Abourt"头。测试如下：
```sh
$response = $browser->request($req);
if ($response->header("Client-Aborted")) {
  warn "Response exceeded maximum size."
}
```

如果你想让你的浏览器对象支持永久连接，调用模块LWP::ConnCache中的conn_cache()来连接一个cache对象。
```sh
use LWP::ConnCache;
$cache = $browser->conn_cache(LWP::ConnCache->new( ));
```
新创建出来的connection cache对象每次只能缓存一个连接。为了可以缓存更多，你需要total_capacity属性。如下展示了如何增长到10个连接：
```sh
$browser->conn_cache->total_capacity(10);
```
缓存所有连接（没有限制）：
```sh
$browser->conn_cache->total_capacity(undef);
```


#### 3.4.2. Request Parameters

The agent( ) attribute gets and sets the string that LWP sends for the User-Agent header:
agent()属性可以设置或者获取LWP发送的User-Agent头的字符串：

```sh
$oldval = $browser->agent([agent_string]);
```

有些网站使用这个字符串来标识具体的浏览器。为了伪装成Netscape来检查之前的服务器是否支持，伪装城Netscape浏览器来访问服务器，用来验证我们是否在使用“浏览器”：
```sh
print "My user agent name is ", $browser->agent( ), ".\n";
$browser->agent("Mozilla/4.76 [en] (Windows NT 5.0; U)");
print "And now I'm calling myself ", $browser->agent( ), "!\n";
My user agent name is libwww-perl/5.60.
And now I'm calling myself Mozilla/4.76 [en] (Windows NT 5.0; U)!
```
from()属性用来设置信息头，头包含用户请求时的email地址：
```sh
$old_address = $browser->from([email_address]);
```
默认值是undef,表示没有From Header。
用户代理对象可以管理发送和接受cookies。使用cookie_jar()方法：
```sh
$old_cj_obj = $browser->cookie_jar([cj_obj])
```
可以设置或者读取HTTP::Cookies对象，这个对象保存所有的浏览器的cookies。默认情况下，当用户代理忽略cookies的情况下，没有cookies jar。
创建临时的cookie jar,这个cookie只在客户端代理对象存在的时候存在：
```sh
$browser->cookie_jar(HTTP::Cookies->new);
```

使用一个文件来保存cookies：
```sh
my $some_file = '/home/mojojojo/cookies.lwp';
$browser->cookie_jar(HTTP::Cookies->new(
  'file' => $some_file, 'autosave' => 1
));
```
Cookies将在单元11的时候介绍。



#### 3.4.3. 协议 (Protocols)

LWP允许我们控制客户代理抓取文档的协议。我们可以只允许某些协议，或者只不允许某些协议。你可以测试LWP某个浏览器对象是否支持某个协议。

protocols_allowed()和protocols_forbidden()方法来明确允许或者拒绝的协议(e.g., FTP 或者 HTTP)在客户代理中使用：
```sh
$aref_maybe = $browser->protocols_allowed([\@protocols]);
$aref_maybe = $browser->protocols_forbidden([\@protocols]);
```
无参数的调用这个函数可以获取一个包含允许或者禁止的协议的数组引用，或者返回undef表明这个属性还没有被设置过。默认情况下，没有任何设置，就是说浏览器对象支持所有LWP所支持的协议。

例如，你在执行一个关于URL名单的程序并且不想解析FTP URL，可以这么写：
```sh
$browser->protocols_forbidden(["ftp"]);
```
然后你就可以执行各种请求，并且所有的ftp URL请求都会自动失败。就是说，如果请求ftp URL，浏览器对象将会返回一个不包含任何实际请求的错误的响应。

你可以通过protocols_allowed方法来指定允许哪些协议。例如，设置浏览器对象只允许http和gopher URL，可以这么写：

```sh
$browser->protocols_allowed(["http", "gopher"]);
```
为了查看LWP和浏览器对象是否支持特定的URL协议，使用is_protocol_supported()方法。如果$browser对象有protocols_allowed列表（大部分情况下没有），如果协议在这个列表中is_protocol_supported返回true。但是如果$browser对象未设置过 protocols_allowed列表，那么只要不在 protocols_forbidden列表中，就返回true。使用如下：
```sh
$boolean = $browser->is_protocol_supported(scheme);
```
举例：

```sh
unless ($browser->is_protocol_supported("https")) {
  warn "Cannot process https:// URLs.\n";
}
```

#### 3.4.4. 重定向 (Redirection)

服务器可以响应请求让客户端重定向到一个新的地址。客户端可以动态的跟踪到重定向的地址去。默认情况下，LWP::UserAgent 模块支持GET HEAD重定向。
requests_redirectable()属性控制客户端动态重定向的方法列表：
```sh
$aref = $browser->requests_redirectable([\@methods]);
```
为了禁止自动重定向，传入一个空数组的引用：
```sh
$browser->requests_redirectable([]);
```
添加POST到重定向方法列表中：

```sh
push @{$browser->requests_redirectable}, 'POST';
```
可以通过request测试一下是否支持重定向：
```sh
$boolean = $browser->redirect_ok(request);
```
如果支持返回true。


#### 3.4.5. 验证 (Authentication)

客户端可以管理多个请求到同一网站的认证信息。credentials()方法可以设置用户名密码用来访问网站的特定域：
```sh
$browser->credentials(host_port, realm, uname, pass);
```
realm参数为一个字符串，指定服务器上限制访问的区域。在交互式浏览中，realm是弹窗上字符串的一部分。例如，如果弹窗上显示“Enter username for Unicode-MailList-Archives at www.unicode.org”，那么realm字符串是Unicode-MailList-Archives。host_port是www.unicode.org:80(注意浏览器上是不会显示HTTP的默认端口80，以及HTTPS的默认端口443)。

发往host_port指定的主机和端口号的请求中都会发送username, password和realm，然后验证。例如：
```sh
$browser->credentials("intranet.example.int:80", "Finances",
                      "fred", "3l1t3");
```
按照上面所写，所用通过80端口的请求都会使用用户名"fred"和密码"3l1t3"来验证域名"Finances"。
关于验证的更多信息，请看第11章。






